MODULE Reception;
REQUIRE Documents, AppMenu, Organization;

NAMESPACE Documents;
//наследуем сущность приходы от сущности Document
CLASS Reception 'Приходы' : Document;

//добавляем свойства, которых нет в базовой сущности
name 'Наименование' = DATA STRING[100](Reception) NONULL ;
price 'Цена' = DATA NUMERIC[10,2] (Reception) NONULL ;

//композиция
supplier = DATA Organization(Reception);
organizationName 'Поставщик'(Reception o) = name(supplier(o));

// контроль данных
unique = GROUP AGGR Reception o BY number(o), date(o), name(o);
CONSTRAINT SETCHANGED(supplier(Reception o)) AND NOT organizationType(supplier(o)) = OrganizationType.supplier
    CHECKED BY supplier[Reception] MESSAGE 'Для приходной накладной организация должна быть поставщиком';

//формы
FORM editReception 'Приход'
    OBJECTS r = Reception PANEL //опция panel - только одна запись
    PROPERTIES (r) number, date, organizationName, name,quantity,price
        EDIT Reception OBJECT r;

DESIGN editReception{
    OBJECTS {
        NEW cntDoc{
            horizontal = TRUE ; 
            caption = 'Накладная';
            height = 60;
            MOVE PROPERTY (number(r));
            MOVE PROPERTY (date(r));
        }
        NEW cntOrg{
            horizontal = TRUE ;
            caption = 'Поставщик';
            height = 60;
            MOVE PROPERTY (organizationName(r)) {caption = 'Фирма';}
        }
        NEW cntRow{
            horizontal = FALSE ;
            caption = 'Строки накладной';
            MOVE PROPERTY (name(r));
            MOVE PROPERTY (quantity(r));
            MOVE PROPERTY (price(r));
        }
    }
}

FORM viewReception 'Приходы'
    OBJECTS r = Reception
    PROPERTIES (r) READONLY number,date,organizationName,name,quantity,price,userName,userDate
    PROPERTIES (r) NEWSESSION NEW ,EDIT ,DELETE ;

//навигатор
NAVIGATOR {
    documents{
        NEW FORM viewReception;
    }
}


